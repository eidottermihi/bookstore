apply plugin: 'jetty'

apply plugin: 'maven-publish'

dependencies {
	compile project(':service')
}

task addBuildInfo {
  doLast {
    def buildInfoFile = new File("$buildDir/buildInfo.properties")
    Properties props = new Properties()
    props.put("version", project.version.toString())
    props.put("timestamp", project.buildTimestamp)
    props.store(buildInfoFile.newWriter(), null)
  }
  // damit buildDir vorhanden ist
  dependsOn classes
}

war {
  dependsOn addBuildInfo
  baseName = "bookstore"
  
  from (buildDir) {
    include 'buildInfo.properties'
    into('WEB-INF/classes')
  }
  
}

ext.fullRepoUrl = "$config.binaryRepository.url/$config.binaryRepository.name"


publishing {
  
  publications {
    webApp(MavenPublication) {
      from components.web
    }
  }
    
  repositories {
    maven {
      url fullRepoUrl
      credentials {
          username = config.binaryRepository.username
          password = config.binaryRepository.password
      }
    }
  }
}
    

